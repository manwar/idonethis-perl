#!/usr/bin/perl -w
use 5.010;
use strict;
use warnings;
use autodie;

# ABSTRACT: Submit an entry to idonethis.com

# VERSION: Generated by DZP::OurPkg::Version

# PODNAME: idone

=head1 SYNOPSIS

    # Submit an item to idonethis.com
    $ idonethis "Installed some cool software today."

    # In ~/.idonethisrc
    [auth]
    user=someuser
    pass=somepass

=head1 DESCRIPTION

This submits items to your personal calendar on idonethis.  All
arguments are concatenated together to form a single string, which
is then submitted to your calendar for the current day (using the
local timezone on your machine).

Patches are extremely welcome.  L<https://github.com/pfenwick/idonethis-perl>

=cut

use WebService::Idonethis;
use Config::Tiny;
use POSIX qw(tzset strftime);

unless (@ARGV) {
    die "Usage: $0 [done item description]\n";
}

my $config = Config::Tiny->read( "$ENV{HOME}/.idonethisrc" );

unless ($config->{auth}{user}) {
    die "Cannot find credentials in $ENV{HOME}/.idonethisrc";
}

my $idt = WebService::Idonethis->new(
    user => $config->{auth}{user},
    pass => $config->{auth}{pass},
);

$idt->set_done( text => "@ARGV" );
